import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: "androidx.navigation.safeargs"

def versionMajor = 1
def versionMinor = 0
def versionPatch = 0
def versionBuild = 0
def versionStage = ""

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.necatisozer.movievault"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode versionMajor * 1000000 + versionMinor * 10000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        if (!versionStage.isEmpty()) {
            versionName "${versionName}-${versionStage}"
        }
        archivesBaseName = "hocanizkim-app-$versionName-$versionCode"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        buildConfigField 'String', "TMDB_API_KEY", getLocalProperty("TMDB_API_KEY")
        buildConfigField "String", "GIT_SHA", "\"${gitSha()}\""
        buildConfigField "String", "BUILD_TIME", "\"${buildTime()}\""
    }
    buildTypes {
        release {
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }
    dataBinding { enabled = true }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    // AndroidX
    implementation 'androidx.appcompat:appcompat:1.1.0-alpha03'
    implementation "androidx.annotation:annotation:1.0.2"
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha3'
    implementation "androidx.recyclerview:recyclerview:1.1.0-alpha03"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation 'com.google.android.material:material:1.1.0-alpha04'

    // KTX
    implementation 'androidx.core:core-ktx:1.1.0-alpha05'
    implementation 'androidx.fragment:fragment-ktx:1.1.0-alpha05'
    implementation 'androidx.collection:collection-ktx:1.1.0-alpha03'

    // Lifecycle
    implementation 'androidx.lifecycle:lifecycle-extensions:2.1.0-alpha03'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.1.0-alpha03'
    kapt "androidx.lifecycle:lifecycle-compiler:2.1.0-alpha03"

    // Navigation
    implementation "android.arch.navigation:navigation-fragment:1.0.0"
    implementation 'android.arch.navigation:navigation-fragment-ktx:1.0.0'
    implementation "android.arch.navigation:navigation-ui:1.0.0"
    implementation 'android.arch.navigation:navigation-ui-ktx:1.0.0'

    // Extensions
    implementation "com.louiscad.splitties:splitties-activities:3.0.0-alpha04"
    implementation "com.louiscad.splitties:splitties-bundle:3.0.0-alpha04"
    implementation "com.louiscad.splitties:splitties-collections:3.0.0-alpha04"
    implementation "com.louiscad.splitties:splitties-fragments:3.0.0-alpha04"
    implementation "com.louiscad.splitties:splitties-fragmentargs:3.0.0-alpha04"
    implementation "com.louiscad.splitties:splitties-material-colors:3.0.0-alpha04"
    implementation "com.louiscad.splitties:splitties-snackbar:3.0.0-alpha04"
    implementation "com.louiscad.splitties:splitties-systemservices:3.0.0-alpha04"
    implementation "com.louiscad.splitties:splitties-toast:3.0.0-alpha04"
    implementation "com.louiscad.splitties:splitties-views:3.0.0-alpha04"
    implementation "com.louiscad.splitties:splitties-views-appcompat:3.0.0-alpha04"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:2.5.0"
    implementation "com.squareup.okhttp3:logging-interceptor:3.14.0"
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.5.0'
    implementation "com.squareup.retrofit2:converter-moshi:2.5.0"

    // Moshi
    implementation "com.squareup.moshi:moshi:1.8.0"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:1.8.0"
    implementation 'com.serjltt.moshi:moshi-lazy-adapters:2.2'

    // Dagger
    implementation 'com.google.dagger:dagger:2.21'
    kapt 'com.google.dagger:dagger-compiler:2.21'
    implementation 'com.google.dagger:dagger-android:2.21'
    implementation 'com.google.dagger:dagger-android-support:2.21'
    kapt 'com.google.dagger:dagger-android-processor:2.21'

    // RxJava
    implementation "io.reactivex.rxjava2:rxjava:2.2.7"
    implementation 'io.reactivex.rxjava2:rxkotlin:2.3.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'

    // Storage
    implementation 'com.github.pakoito:RxPaper2:1.4.0'

    // Util
    implementation 'com.github.bumptech.glide:glide:4.9.0'
    kapt 'com.github.bumptech.glide:compiler:4.9.0'
    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation "com.jakewharton.threetenabp:threetenabp:1.2.0"

    // View
    implementation 'com.yarolegovich:discrete-scrollview:1.4.9'

    // Debug
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'com.facebook.stetho:stetho:1.5.1'
    implementation 'com.facebook.stetho:stetho-okhttp3:1.5.1'

    // Test
    testImplementation 'junit:junit:4.13-beta-1'
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
}

static def gitSha() {
    return 'git rev-parse --short HEAD'.execute().text.trim()
}

static def buildTime() {
    def df = new SimpleDateFormat("yyyy-MM-dd'T'HH:mm'Z'")
    return df.format(new Date())
}
